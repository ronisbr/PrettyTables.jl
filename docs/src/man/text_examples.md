# Text Back End Examples

```@meta
CurrentModule = PrettyTables
```

```@setup text_examples
using PrettyTables
```

In the following, it is presented how the following matrix can be printed using the text
back end.

```@repl text_examples
data = Any[
    1    false      1.0     0x01
    2     true      2.0     0x02
    3    false      3.0     0x03
    4     true      4.0     0x04
    5    false      5.0     0x05
    6     true      6.0     0x06
]
```

---

```julia
julia> pretty_table(data)
```

```@setup text_examples
str = pretty_table(String, data; color = true)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00001.svg`)

run(`rm tmp`)
```

![Text back end example 1](./text_example_00001.svg)

---

```julia-repl
julia> pretty_table(data; border_crayon = crayon"yellow")
```

```@setup text_examples
str = pretty_table(String, data; border_crayon = crayon"yellow", color = true)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00002.svg`)

run(`rm tmp`)
```

![Text back end example 2](./text_example_00002.svg)

---

```julia-repl
julia> pretty_table(
    data;
    border_crayon = crayon"bold yellow",
    header_crayon = crayon"bold green",
    tf = tf_simple,
)
```

```@setup text_examples
str = pretty_table(
    String,
    data;
    border_crayon = crayon"bold yellow",
    color = true,
    header_crayon = crayon"bold green",
    tf = tf_simple,
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00003.svg`)

run(`rm tmp`)
```

![Text back end example 3](./text_example_00003.svg)

---

```julia-repl
julia> pretty_table(data; tf = tf_markdown, show_row_number = true)
```

```@setup text_examples
str = pretty_table(
    String,
    data;
    color = true,
    show_row_number = true,
    tf = tf_markdown,
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00004.svg`)

run(`rm tmp`)
```

![Text back end example 4](./text_example_00004.svg)

---

The following example shows how `formatters` can be used to change how elements are printed.

```julia
julia> formatter = (v, i, j) -> begin
           if j != 2
               return isodd(i) ? i : 0
           else
               return v
           end
       end

julia> pretty_table(data; tf = tf_ascii_rounded, formatters = formatter)
```

```@setup text_examples
formatter = (v, i, j) -> begin
    if j != 2
        return isodd(i) ? i : 0
    else
        return v
    end
end

str = pretty_table(
    String,
    data;
    color = true,
    formatters = formatter,
    tf = tf_ascii_rounded,
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00005.svg`)

run(`rm tmp`)
```

![Text back end example 5](./text_example_00005.svg)

---

The following example indicates how `highlighters` can be used to highlight the lowest and
highest element in the data considering the columns 1, 3, and 5:

```julia-repl
julia> h1 = Highlighter(
    (data, i, j) -> j in (1, 3, 4) && data[i, j] == maximum(data[2:end, [1, 3, 4]]),
    bold       = true,
    foreground = :blue )

julia> h2 = Highlighter(
    (data,i,j)->j in (1, 3, 4) && data[i, j] == minimum(data[2:end,[1, 3, 4]]),
    bold       = true,
    foreground = :red
)

julia> pretty_table(data; highlighters = (h1, h2))
```

```@setup text_examples
h1 = Highlighter(
    (data, i, j) -> j in (1, 3, 4) && data[i, j] == maximum(data[2:end, [1, 3, 4]]),
    bold       = true,
    foreground = :blue )

h2 = Highlighter(
    (data,i,j)->j in (1, 3, 4) && data[i, j] == minimum(data[2:end,[1, 3, 4]]),
    bold       = true,
    foreground = :red
)

str = pretty_table(
    String,
    data;
    color = true,
    highlighters = (h1, h2)
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00006.svg`)

run(`rm tmp`)
```

![Text back end example 6](./text_example_00006.svg)

---

Since this package has support to the API defined by
[Tables.jl](https://github.com/JuliaData/Tables.jl), many formats, _e.g_
[DataFrames.jl](https://github.com/JuliaData/DataFrames.jl), can be pretty printed:

```julia-repl
julia> using DataFrames

julia> df = DataFrame(A = 1:2:20, B = rand(10), C = rand(10))

julia> pretty_table(
    df;
    formatters = ft_printf("%.3f", [2, 3]),
    highlighters = (hl_lt(0.2), hl_gt(0.8))
)
```

```@setup text_examples
using DataFrames

df = DataFrame(A = 1:2:20, B = rand(10), C = rand(10))

str = pretty_table(
    String,
    df;
    color = true,
    formatters = ft_printf("%.3f", [2, 3]),
    highlighters = (hl_lt(0.2), hl_gt(0.8))
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00007.svg`)

run(`rm tmp`)
```

![Text back end example 7](./text_example_00007.svg)

---

You can use `body_hlines` keyword to divide the table into interesting parts:

```julia-repl
julia> pretty_table(data; body_hlines = [2, 4])
```

```@setup text_examples
str = pretty_table(
    String,
    data;
    body_hlines = [2, 4],
    color = true,
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00008.svg`)

run(`rm tmp`)
```

![Text back end example 8](./text_example_00008.svg)

---

If you want to break lines inside the cells, you can set the keyword `linebreaks` to `true`.
Hence, the characters `\n` will cause a line break inside the cell.

```julia-repl
julia> text = [
    "This line contains\nthe velocity [m / s]" 10.0
    "This line contains\nthe acceleration [m / s^2]" 1.0
    "This line contains\nthe time from the\nbeginning of the simulation" 10
]

julia> pretty_table(text; linebreaks = true, body_hlines = [1, 2, 3])
```

```@setup text_examples
text = [
    "This line contains\nthe velocity [m / s]" 10.0
    "This line contains\nthe acceleration [m / s^2]" 1.0
    "This line contains\nthe time from the\nbeginning of the simulation" 10
]

str = pretty_table(
    String,
    text;
    body_hlines = [1, 2, 3],
    color = true,
    linebreaks = true,
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00009.svg`)

run(`rm tmp`)
```

![Text back end example 9](./text_example_00009.svg)

---

The keyword `show_header` can be used to suppress the header, which leads to a very
simplistic, compact format.

```julia-repl
julia> pretty_table(data; tf = tf_borderless, show_header = false)
```

```@setup text_examples
str = pretty_table(
    String,
    data;
    color = true,
    show_header = false,
    tf = tf_borderless
)

write("tmp", str)

run(`ansitoimg --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00010.svg`)

run(`rm tmp`)
```

![Text back end example 10](./text_example_00010.svg)

---

By default, if the data is larger than the display, it will be cropped to fit it. This can
be changed by using the keywords `crop` and `display_size`.

```julia
julia> data = rand(100, 10)

julia> pretty_table(data, highlighters = (hl_gt(0.5),))
```

```@setup text_examples
data2 = rand(100, 10)

str = pretty_table(
    String,
    data2;
    color = true,
    crop = :both,
    display_size = (20, 89),
    highlighters = (hl_gt(0.5),)
)

write("tmp", str)

run(`ansitoimg --wide --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00011.svg`)

run(`rm tmp`)
```

![Text back end example 11](./text_example_00011.svg)

---

You can use the keyword `columns_width` to select the width of each column, so that the data
is cropped to fit the available space.

```julia-repl
julia> mat = rand(100, 4)

julia> pretty_table(
    mat;
    highlighters = hl_gt(0.5),
    columns_width = [7, -1, 7, 8],
    compact_printing = false
)
```

```@setup text_examples
mat = rand(100, 4)

str = pretty_table(
    String,
    mat;
    color = true,
    columns_width = [7, -1, 7, 8],
    compact_printing = false,
    crop = :both,
    display_size = (20, 89),
    highlighters = hl_gt(0.5),
)

write("tmp", str)

run(`ansitoimg --wide --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00012.svg`)

run(`rm tmp`)
```

![Text back end example 12](./text_example_00012.svg)

---

If you want to save the printed table to a file, you can do:

```julia-repl
julia> open("output.txt", "w") do f
            pretty_table(f,data)
       end
```

---

This package can also be used to create data reports in text format:

```julia-repl
julia> data = [
    "Torques" "" "" "";
    "Atmospheric drag" "."^10 10 "10⁻⁵ Nm";
    "Gravity gradient" "."^10 3 "10⁻⁵ Nm";
    "Solar radiation pressure" "."^10 0.1 "10⁻⁵ Nm";
    "Total" "."^10 13.1 "10⁻⁵ Nm";
    "" "" "" ""
    "Angular momentum" "" "" "";
    "Atmospheric drag" "."^10 6.5 "Nms";
    "Gravity gradient" "."^10 3.0 "Nms";
    "Solar radiation pressure" "."^10 1.0 "Nms";
    "Total" "."^10 10.5 "Nms"
]

julia> pretty_table(
    data;
    body_hlines        = [1,7],
    body_hlines_format = Tuple('─' for _ = 1:4),
    cell_alignment     = Dict((1, 1) => :l, (7, 1) => :l),
    formatters         = ft_printf("%10.1f", 2),
    show_header        = false,
    tf                 = tf_borderless,
    highlighters       = (
      hl_cell([(1, 1); (7, 1)], crayon"bold"),
      hl_col(2, crayon"dark_gray"),
      hl_row([5, 11], crayon"bold yellow")
      ),
)
```

```@setup text_examples
data3 = [
    "Torques" "" "" "";
    "Atmospheric drag" "."^10 10 "10⁻⁵ Nm";
    "Gravity gradient" "."^10 3 "10⁻⁵ Nm";
    "Solar radiation pressure" "."^10 0.1 "10⁻⁵ Nm";
    "Total" "."^10 13.1 "10⁻⁵ Nm";
    "" "" "" ""
    "Angular momentum" "" "" "";
    "Atmospheric drag" "."^10 6.5 "Nms";
    "Gravity gradient" "."^10 3.0 "Nms";
    "Solar radiation pressure" "."^10 1.0 "Nms";
    "Total" "."^10 10.5 "Nms"
]

str = pretty_table(
    String,
    data3;
    body_hlines        = [1,7],
    body_hlines_format = Tuple('─' for _ = 1:4),
    cell_alignment     = Dict((1, 1) => :l, (7, 1) => :l),
    color              = true,
    formatters         = ft_printf("%10.1f", 2),
    show_header        = false,
    tf                 = tf_borderless,
    highlighters       = (
        hl_cell([(1, 1); (7, 1)], crayon"bold"),
        hl_col(2, crayon"dark_gray"),
        hl_row([5, 11], crayon"bold yellow")
    ),
)

write("tmp", str)

run(`ansitoimg --width 60 --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00013.svg`)

run(`rm tmp`)
```

![Text back end example 13](./text_example_00013.svg)

---

The highlighters API can be used to dynamically highlight cells. In the next example, it is
shown how the package [ColorSchemes.jl](https://github.com/JuliaGraphics/ColorSchemes.jl)
can be integrated to build a table with a color map (the following example will be displayed
better in a terminal that supports 24-bit color):

```julia-repl
julia> using ColorSchemes

julia> data = [sind(x) * cosd(y) for x in 0:10:180, y in 0:10:180]

julia> hl = Highlighter(
    (data, i, j) -> true,
    (h, data, i, j) -> begin
         color = get(colorschemes[:coolwarm], data[i, j], (-1, 1))
         return Crayon(foreground = (
             round(Int, color.r * 255),
             round(Int, color.g * 255),
             round(Int, color.b * 255))
         )
     end
)

julia> pretty_table(
    data;
    header = ["x = $(x)°" for x = 0:10:180],
    row_labels = ["y = $(y)°" for y = 0:10:180],
    highlighters = hl,
    formatters = ft_printf("%.2f")
)
```

```@setup text_examples
using ColorSchemes

data4 = [sind(x) * cosd(y) for x in 0:10:180, y in 0:10:180]

hl = Highlighter(
    (data, i, j) -> true,
    (h, data, i, j) -> begin
         color = get(colorschemes[:coolwarm], data[i, j], (-1, 1))
         return Crayon(foreground = (
             round(Int, color.r * 255),
             round(Int, color.g * 255),
             round(Int, color.b * 255))
         )
     end
)

str = pretty_table(
    String,
    data4;
    color        = true,
    formatters   = ft_printf("%.2f"),
    header       = ["x = $(x)°" for x = 0:10:180],
    highlighters = hl,
    row_labels   = ["y = $(y)°" for y = 0:10:180],
)

write("tmp", str)

run(`ansitoimg --width 211 --title "PrettyTables.jl (generated by AnsiToImg)" tmp text_example_00014.svg`)

run(`rm tmp`)
```

![Text back end example 14](./text_example_00014.svg)
